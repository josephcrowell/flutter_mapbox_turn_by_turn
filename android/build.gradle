group 'au.com.annon.flutter_mapbox_turn_by_turn'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '2.2.0'
    repositories {
        google()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2' }
        maven {
            url 'https://api.mapbox.com/downloads/v2/releases/maven'
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username = "mapbox"
                password = project.hasProperty('MAPBOX_DOWNLOADS_TOKEN') ? project.property('MAPBOX_DOWNLOADS_TOKEN') : System.getenv('MAPBOX_DOWNLOADS_TOKEN')
                if (password == null || password == "") {
                    throw new GradleException("MAPBOX_DOWNLOADS_TOKEN isn't set. Set it to the project properties or to the enviroment variables.")
                }
            }
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.13.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath "com.mapbox.gradle.plugins:access-token:0.4.0"
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2' }
        maven {
            url 'https://api.mapbox.com/downloads/v2/releases/maven'
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username = "mapbox"
                password = project.hasProperty('MAPBOX_DOWNLOADS_TOKEN') ? project.property('MAPBOX_DOWNLOADS_TOKEN') : System.getenv('MAPBOX_DOWNLOADS_TOKEN')
                if (password == null || password == "") {
                    throw new GradleException("MAPBOX_DOWNLOADS_TOKEN isn't set. Set it to the project properties or to the enviroment variables.")
                }
            }
        }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
android {
    compileSdkVersion 36

    if (project.android.hasProperty("namespace")) {
        namespace 'au.com.annon.flutter_mapbox_turn_by_turn'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        minSdkVersion 23
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
        vectorDrawables.useSupportLibrary = true
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        debug {
            testCoverageEnabled = false
            manifestPlaceholders = [enableCrashReporting: "false"]
        }
        release {
            minifyEnabled true
            manifestPlaceholders = [enableCrashReporting: "true"]
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation "androidx.core:core-ktx:1.17.0"

    implementation 'androidx.annotation:annotation:1.9.1'
    implementation "androidx.appcompat:appcompat:1.7.1"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:2.2.1"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    def lifecycle_version = "2.9.3"
    //noinspection KtxExtensionAvailable
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.multidex:multidex:2.0.1"

    implementation 'com.google.code.gson:gson:2.13.2'
    implementation "com.google.android.gms:play-services-location:21.3.0"
    implementation "com.google.android.material:material:1.13.0"
    implementation 'com.google.auto.value:auto-value-annotations:1.11.0'

    implementation "com.mapbox.navigationcore:android-ndk27:3.12.0-rc.1"
    implementation "com.mapbox.navigationcore:ui-components-ndk27:3.12.0-rc.1"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.10.2"

    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.14"

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
}
